/*
 * LunaticSMTP
 * Copyright (C) 2016  Anton Larionov
 *
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this program.  If not, see <http://www.gnu.org/licenses/>.
 */

plugins {
    id 'java'
    id 'idea'
    id 'org.openjfx.javafxplugin' version '0.0.8'
    id 'nebula.ospackage' version '8.4.1'
    id 'com.github.ben-manes.versions' version '0.27.0'
}

repositories {
    mavenCentral()
}

sourceCompatibility = 11
targetCompatibility = 11

ext {
    applicationName = 'lunaticsmtp'
    baseVersion = '0.5.0'
    mainClass = 'com.gitlab.anlar.lunatic.Lunatic'
    author = 'Anton Larionov'

    buildTime = new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
    gitRevision = 'git rev-parse HEAD'.execute().text.trim()
    gitShortRevision = 'git rev-parse --short HEAD'.execute().text.trim()
}

idea {
    project {
        jdkName = 'openjdk-11'
        vcs = 'Git'
    }
}

javafx {
    version = "11"
    modules = ['javafx.controls', 'javafx.fxml', 'javafx.web']
}

dependencies {
    compile 'org.slf4j:slf4j-api:1.7.21'
    compile 'org.slf4j:slf4j-log4j12:1.7.21'

    compile 'org.apache.commons:commons-lang3:3.4'
    compile 'com.beust:jcommander:1.48'
    compile 'org.subethamail:subethasmtp:3.1.7'
    compile 'org.controlsfx:controlsfx:11.0.0'

    testCompile 'junit:junit:4.12'
    testCompile 'org.testfx:testfx-core:4.0.16-alpha'
    testCompile 'org.testfx:testfx-junit:4.0.15-alpha'
}

test {
    if (System.properties['test.profile'] == 'headless') {
        exclude '**/gui/*'
    }
}

tasks.withType(Jar) {
    manifest {
        attributes(
                'Created-By': "${System.getProperty("java.runtime.version")} (${System.getProperty("java.vm.vendor")})",
                'Build-Time': buildTime,

                'Specification-Title': applicationName,
                'Specification-Version': baseVersion,
                'Specification-Vendor': author,

                'Implementation-Title': applicationName,
                'Implementation-Version': baseVersion + '-' + gitRevision,
                'Implementation-Vendor': author,

                'Main-Class': mainClass
        )
    }
}

task('release', type: Jar, dependsOn: 'check', description: 'Assembles a jar archive with application.') {
    archiveBaseName = applicationName
    archiveVersion = 'v' + baseVersion

    Properties props = new Properties()
    props.setProperty('version', baseVersion)
    props.setProperty('gitShortRevision', gitShortRevision)

    mkdir("$buildDir/resources/main")
    File propsFile = file("$buildDir/resources/main/version.properties")
    props.store(propsFile.newWriter(), null)

    from {
        configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    }
    with jar
}

task releaseDeb(type: Deb, dependsOn: 'release', description: 'Assembles deb-package.') {
    packageName = 'lunaticsmtp'
    version = baseVersion
    release = 1
    user = 'root'

    license = 'GPLv3'
    maintainer = author + ' <contact-via-project-issue-tracker@github.com>'
    url = 'https://github.com/anlar/lunaticsmtp'
    summary = 'dummy SMTP server with GUI'
    packageDescription = 'Simple JavaFX SMTP server for testing email sending applications.'

    requires('default-jre', '2:1.11', GREATER | EQUAL).or('java11-runtime')
    requires('openjfx')

    customField([
            'Vcs-Browser': 'https://github.com/anlar/lunaticsmtp',
    ])

    from("$buildDir/libs/$applicationName-v${baseVersion}.jar") {
        into '/usr/share/lunaticsmtp'
        rename("$applicationName-v${baseVersion}.jar", "${applicationName}.jar")
        fileMode 0644
    }

    from("package/linux/lunaticsmtp") {
        into '/usr/bin'
        fileMode 0755
    }

    from("package/linux/lunaticsmtp.desktop") {
        into '/usr/share/applications'
        fileMode 0644
    }

    from("package/linux/lunaticsmtp.png") {
        into '/usr/share/pixmaps'
        fileMode 0644
    }

    from("package/linux/copyright") {
        into "/usr/share/doc/$packageName"
        fileMode 0644
    }

    from("package/linux/lunaticsmtp.1") {
        into "/usr/share/man/man1"
        fileMode 0644
    }
}
